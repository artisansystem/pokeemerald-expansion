mapscripts KingsisleInstitute_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_KINGSISLE_INTRO_STATE, 1: KingsisleInstitute_EventScript_StepOffTruck
    ]
}

raw `
CommonName_Text_Student:
    .string "Student$"

UniqueName_Text_Mercury:
    .string "Mercury$"
`

script DormSign{
    msgbox("SOLGALEO HALL", MSGBOX_SIGN)
}

script NadiaLab{
	msgbox("PROFESSOR NADIA'S LAB", MSGBOX_SIGN)
}

script SecurityOffice{
    msgbox("KINGSISLE SECURITY OFFICE", MSGBOX_SIGN)
}

script AcademySign{
    msgbox("ACADEMY THIS WAY", MSGBOX_SIGN)
}

script Kingsisle_MagicGuy{
    speakername(CommonName_Text_Student)
    msgbox(MagicIsAmazing, MSGBOX_NPC)
}

text MagicIsAmazing{
    format("Magic is amazing!\p I heard that students at the top of their class are able to travel to other Underground sectors using teleportation circles!")
}

script KingsisleInstitute_EventScript_StepOffTruck {
    lockall
    applymovement(LOCALID_PLAYER, moves(set_invisible))
    call(KingsisleInstitute_EventScript_GoInsideWithMercury)
    setflag(FLAG_HIDE_KINGSISLE_TRUCK)
    warpsilent(MAP_KINGSISLE_DORM_1F, 9, 9)
    waitstate
    releaseall
    end
}

script KingsisleInstitute_EventScript_GoInsideWithMercury {
    delay(15)
    playse(SE_LEDGE)
    applymovement(LOCALID_PLAYER, KingsisleInstitute_Movement_PlayerStepOffTruck)
    waitmovement(0)
    applymovement(LOCALID_KINGSISLE_MERCURY, KingsisleInstitute_Movement_MercuryWalkToPlayer)
    waitmovement(0)
    speakername(UniqueName_Text_Mercury)
    msgbox(KingsisleInstitute_Text_YourNewHome, MSGBOX_DEFAULT)
    closemessage
    applymovement(LOCALID_KINGSISLE_MERCURY, KingsisleInstitute_Movement_MercuryWalkInside)
    applymovement(LOCALID_PLAYER, KingsisleInstitute_Movement_PlayerWalkInside)
    waitmovement(0)
    setflag(FLAG_HIDE_MERCURY_OUTSIDE)
    setvar(VAR_KINGSISLE_INTRO_STATE, 2)
    clearflag(FLAG_HIDE_MAP_NAME_POPUP)
    return
}

movement KingsisleInstitute_Movement_PlayerStepOffTruck {
    set_visible
    jump_right
	delay_16
	delay_16
	delay_16
	step_end
}

movement KingsisleInstitute_Movement_MercuryWalkToPlayer {
    walk_down
    walk_down
    walk_in_place_fast_left
    step_end
}

text KingsisleInstitute_Text_YourNewHome {
    format("Alright, kid, we're here.\p This is the first-year dorm, you'll stay here until you pick a college. Let's head inside, yeah?")
}

movement KingsisleInstitute_Movement_MercuryWalkInside {
    walk_up * 3
    walk_right * 3
    walk_up * 2
    set_invisible
    step_end
}

movement KingsisleInstitute_Movement_PlayerWalkInside {
    delay_16
    walk_up * 3
    walk_right * 4
    walk_up * 2
    step_end
}

